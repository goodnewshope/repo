DECLARE @date CHAR(8)
SET @date = (SELECT CONVERT(char(8), GETDATE(), 112))

DECLARE @path VARCHAR(1000)
SET @path = '\\JHPFS\DB_Backup\HPINFSQLCL\Migrationjeff\Full\' --- you need to change this to you shar location this full backup 

Declare @DiffPath varchar(1000)
set @DiffPath = '\\JHPFS\DB_Backup\HPINFSQLCL\Migrationjeff\Diff\' -- you need to change this to you shar location this diff backup 


;WITH MoveCmdCTE ( DatabaseName, MoveCmd )
          AS ( SELECT DISTINCT
                        DB_NAME(database_id) ,
                        STUFF((SELECT   ' ' + CHAR(13)+', MOVE ''' + name + ''''
                                        + CASE Type
                                            WHEN 0 THEN ' TO ''E:\Data\'
                                            ELSE ' TO ''F:\Logs\'
                                          END
                                        + REVERSE(LEFT(REVERSE(physical_name),
                                                       CHARINDEX('\',
                                                              REVERSE(physical_name),
                                                              1) - 1)) + ''''
                               FROM     sys.master_files sm1
                               WHERE    sm1.database_id = sm2.database_ID
                        FOR   XML PATH('') ,
                                  TYPE).value('.', 'varchar(max)'), 1, 1, '') AS MoveCmd
               FROM     sys.master_files sm2
  )
SELECT
	
'BACKUP DATABASE [' + name + '] TO DISK = ''' + @path + '' + name + '_' + @date + '.bak'' WITH COMPRESSION, STATS=5' as [HPINFSQLCL BackupCmd],
'RESTORE DATABASE ['+ name + '] FROM DISK = N''' + @path + '' + name + '_' + @date + '.bak'' WITH  FILE = 1 ' + movecmdCTE.MoveCmd + ',  NORECOVERY,  NOUNLOAD, REPLACE,  STATS = 5'  as [JHPINFSQLPRDPB RestoreNoRecover],
'BACKUP DATABASE [' + name + '] TO  DISK =  N''' + @DiffPath + '' + name + '_' + @date + '_DIFF.bak'' WITH DIFFERENTIAL , NOFORMAT, NOINIT,  NAME = N''' + name + '-Diff Database Backup'', SKIP, NOREWIND, NOUNLOAD,  STATS = 10 ' as [HPINFSQLCL DiffBackupCmd],
--'RESTORE DATABASE ['+ name + '] FROM  DISK = N''' + @DiffPath + '' + name + '_' + @date + '_DIFF.bak'' WITH NORECOVERY, REPLACE, STATS=5 ' + movecmdCTE.MoveCmd as RestoreDiff
'RESTORE DATABASE ['+ name + '] FROM  DISK = N''' + @DiffPath + '' + name + '_' + @date + '_DIFF.bak'' WITH  FILE = 1,  NORECOVERY,  NOUNLOAD,  STATS = 5' as [JHPINFSQLPRDPB RestoreDiff],
'RESTORE DATABASE ['+ name + '] WITH RECOVERY;' as [JHPINFSQLPRDPB RecoverDB]
FROM sys.databases d
	INNER JOIN MoveCMDCTE ON d.name = movecmdcte.databasename

WHERE 1=1
and d.name not in ('master', 'model', 'msdb', 'tempdb', 'resource', 'ReportServer', 'ReportServerTempDB', 'DBA')
--and d.name = 'Audit'
GO


Note i need to run this select * from sys.dm_server_services on each server to know the service aact and give the both full backup path and diff backup path so they can have access 



 
